/*
 * Copyright 2011, Blender Foundation.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
 */

#include "stdosl.h"
#include "node_texture.h"

/* Wood */

float wood(point p, float size, string type, string wave, string basis, int hard, float turb)
{
	float x = p[0];
	float y = p[1];
	float z = p[2];

	float result = 0.0;

	if(type == "Bands") {
		result = noise_wave(wave, (x + y + z)*10.0);
	}
	else if(type == "Rings") {
		result = noise_wave(wave, sqrt(x*x + y*y + z*z)*20.0);
	}
	else if (type == "Band Noise") {
		float wi = turb*noise_basis_hard(p/size, basis, hard);
		result = noise_wave(wave, (x + y + z)*10.0 + wi);
	}
	else if (type == "Ring Noise") {
		float wi = turb*noise_basis_hard(p/size, basis, hard);
		result = noise_wave(wave, sqrt(x*x + y*y + z*z)*20.0 + wi);
	}

	return result;
}

shader node_wood_texture(
	string Type = "Bands",
	string Wave = "Sine",
	string Basis = "Perlin",
	int Hard = 0,
	float Size = 0.25,
	float Turbulence = 5.0,
	point Vector = P,
	output float Fac = 0.0)
{
	float size = nonzero(Size, 1e-5);
	Fac = wood(Vector, size, Type, Wave, Basis, Hard, Turbulence);
}

