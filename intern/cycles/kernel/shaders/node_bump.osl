/*
 * Copyright 2011, Blender Foundation.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
 */

#include "stdosl.h"

/* "Bump Mapping Unparameterized Surfaces on the GPU"
 * Morten S. Mikkelsen, 2010 */

surface node_bump(
	normal NormalIn = N,
	float Strength = 0.0,
	float SampleCenter = 0.0,
	float SampleX = 0.0,
	float SampleY = 0.0,
	output normal NormalOut = N)
{
	/* get surface tangents from normal */
	vector dPdx = Dx(P);
	vector dPdy = Dy(P);

	vector Rx = cross(dPdy, NormalIn);
	vector Ry = cross(NormalIn, dPdx);

	/* compute surface gradient and determinant */
	float det = dot(dPdx, Rx);
	vector surfgrad = (SampleX - SampleCenter) * Rx + (SampleY - SampleCenter) * Ry;

	float absdet = fabs(det);

	float strength = clamp(Strength, 0.0, 1.0);
	
	/* compute and output perturbed normal */
	NormalOut = normalize(absdet * NormalIn - sign(det) * surfgrad);
	NormalOut = normalize(strength*NormalOut + (1.0 - strength)*NormalIn);
}

