/*
 * Copyright 2011, Blender Foundation.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
 */

#include "stdosl.h"

float safe_divide(float a, float b)
{
	float result;

	if (b == 0.0)
		result = 0.0;
	else
		result = a / b;
	
	return result;
}

float safe_modulo(float a, float b)
{
	float result;

	if (b == 0.0)
		result = 0.0;
	else
		result = fmod(a, b);
	
	return result;
}

float safe_log(float a, float b)
{
	if (a < 0.0 || b < 0.0)
		return 0.0;
	
	return log(a) / log(b);
}

shader node_math(
	string type = "Add",
	int Clamp = 0,
	float Value1 = 0.0,
	float Value2 = 0.0,
	output float Value = 0.0)
{
	/* OSL asin, acos, pow check for values that could give rise to nan */

	if (type == "Add")
		Value = Value1 + Value2;
	else if (type == "Subtract")
		Value = Value1 - Value2;
	else if (type == "Multiply")
		Value = Value1 * Value2;
	else if (type == "Divide")
		Value = safe_divide(Value1, Value2);
	else if (type == "Sine")
		Value = sin(Value1);
	else if (type == "Cosine")
		Value = cos(Value1);
	else if (type == "Tangent")
		Value = tan(Value1);
	else if (type == "Arcsine")
		Value = asin(Value1);
	else if (type == "Arccosine")
		Value = acos(Value1);
	else if (type == "Arctangent")
		Value = atan(Value1);
	else if (type == "Power")
		Value = pow(Value1, Value2);
	else if (type == "Logarithm")
		Value = safe_log(Value1, Value2);
	else if (type == "Minimum")
		Value = min(Value1, Value2);
	else if (type == "Maximum")
		Value = max(Value1, Value2);
	else if (type == "Round")
		Value = floor(Value1 + 0.5);
	else if (type == "Less Than")
		Value = Value1 < Value2;
	else if (type == "Greater Than")
		Value = Value1 > Value2;
	else if (type == "Modulo")
		Value = safe_modulo(Value1, Value2);

	if (Clamp)
		Value = clamp(Value1, 0.0, 1.0);
}

