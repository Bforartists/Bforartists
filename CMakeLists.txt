# $Id$
# ***** BEGIN GPL LICENSE BLOCK *****
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software Foundation,
# Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#
# The Original Code is Copyright (C) 2006, Blender Foundation
# All rights reserved.
#
# The Original Code is: all of this file.
#
# Contributor(s): Jacques Beaurain.
#
# ***** END GPL LICENSE BLOCK *****

#-----------------------------------------------------------------------------
# We don't allow in-source builds. This causes no end of troubles because 
# all out-of-source builds will use the CMakeCache.txt file there and even 
# build the libs and objects in it. It will also conflict with the current 
# Makefile system for Blender

IF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})
MESSAGE(FATAL_ERROR "CMake generation for blender is not allowed within the source directory! 
Remove the CMakeCache.txt file and try again from another folder, e.g.: 

   rm CMakeCache.txt 
   cd ..
   mkdir cmake-make 
   cd cmake-make
   cmake -G \"Unix Makefiles\" ../blender
")
ENDIF(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})

CMAKE_MINIMUM_REQUIRED(VERSION 2.4)
PROJECT(Blender)

#-----------------------------------------------------------------------------
# Redirect output files

SET(EXECUTABLE_OUTPUT_PATH ${CMAKE_BINARY_DIR}/bin)
SET(LIBRARY_OUTPUT_PATH  ${CMAKE_BINARY_DIR}/lib)

#-----------------------------------------------------------------------------
# Set default config options
OPTION(WITH_PLAYER		"Build Player"						OFF)
OPTION(WITH_GAMEENGINE		"Enable Game Engine"					ON)
OPTION(WITH_BULLET		"Enable Bullet (Physics Engine)"			ON)
OPTION(WITH_INTERNATIONAL	"Enable I18N   (International fonts and text)"		ON)
OPTION(WITH_ELBEEM		"Enable Elbeem (Fluid Simulation)"			ON)
OPTION(WITH_QUICKTIME		"Enable Quicktime Support"				OFF)
OPTION(WITH_OPENEXR		"Enable OpenEXR Support (http://www.openexr.com)"	ON)
OPTION(WITH_DDS			"Enable DDS Support"                                   	ON)
OPTION(WITH_FFMPEG		"Enable FFMPeg Support (http://ffmpeg.mplayerhq.hu/)"	OFF)
OPTION(WITH_PYTHON		"Enable Embedded Python API"				ON)
OPTION(WITH_SDL			"Enable SDL for sound and joystick support"				ON)
OPTION(WITH_OPENJPEG		"Enable OpenJpeg Support (http://www.openjpeg.org/)"	OFF)
OPTION(WITH_OPENAL		"Enable OpenAL Support (http://www.openal.org)"		ON)
OPTION(WITH_OPENMP		"Enable OpenMP (has to be supported by the compiler)"	OFF)
OPTION(WITH_WEBPLUGIN		"Enable Web Plugin (Unix only)"				OFF)
OPTION(WITH_FFTW3		"Enable FFTW3 support"				OFF)

IF(NOT WITH_GAMEENGINE AND WITH_PLAYER)
  MESSAGE("WARNING: WITH_PLAYER needs WITH_GAMEENGINE")
ENDIF(NOT WITH_GAMEENGINE AND WITH_PLAYER)

# For alternate Python locations the commandline can be used to override detected/default cache settings, e.g:
# On Unix: 
#   cmake -D PYTHON_LIB=/usr/local/lib/python2.3/config/libpython2.3.so -D PYTHON_INC=/usr/local/include/python2.3 -D PYTHON_BINARY=/usr/local/bin/python2.3 -G "Unix Makefiles" ../blender
# On Macs: 
#   cmake -D PYTHON_INC=/System/Library/Frameworks/Python.framework/Versions/2.5/include/python2.5 -D PYTHON_LIBPATH=/System/Library/Frameworks/Python.framework/Versions/2.5/lib/python2.5/config -D PYTHON_BINARY=/System/Library/Frameworks/Python.framework/Versions/2.5/bin/python2.5 -G Xcode ../blender
#
# When changing any of this remember to update the notes in doc/blender-cmake.txt

#-----------------------------------------------------------------------------
# Load some macros.
INCLUDE(CMake/macros.cmake)

#-----------------------------------------------------------------------------
#Platform specifics

IF(UNIX AND NOT APPLE)
  IF(WITH_OPENAL)
    FIND_PACKAGE(OpenAL)
    IF(OPENAL_FOUND)
      SET(WITH_OPENAL ON)
      SET(OPENAL_LIB ${OPENAL_LIBRARY})
      SET(OPENAL_INC ${OPENAL_INCLUDE_DIR})
    ELSE(OPENAL_FOUND)
      SET(WITH_OPENAL OFF)
    ENDIF(OPENAL_FOUND)
  ENDIF(WITH_OPENAL)

  FIND_LIBRARY(INTL_LIBRARY
    NAMES intl
    PATHS
    /sw/lib
  )
  FIND_LIBRARY(ICONV_LIBRARY
    NAMES iconv
    PATHS
    /sw/lib
  )
  IF(INTL_LIBRARY AND ICONV_LIBRARY)
    SET(GETTEXT_LIB ${INTL_LIBRARY} ${ICONV_LIBRARY})
  ENDIF(INTL_LIBRARY AND ICONV_LIBRARY)
  
  FIND_PATH(FREETYPE_INC
    freetype
    PATHS
    /usr/local/include/freetype2
    /usr/include/freetype2
    /sw/include/freetype2
    /opt/local/include/freetype2
    /opt/csw/include/freetype2
    /opt/include/freetype2
    NO_DEFAULT_PATH
  )
  SET(FREETYPE_LIB freetype)

  FIND_PACKAGE(PythonLibs)
  SET(PYTHON_INC "${PYTHON_INCLUDE_PATH}" CACHE STRING "")
  SET(PYTHON_LIB "${PYTHON_LIBRARIES}" CACHE STRING "")
  FIND_PACKAGE(PythonInterp)
  SET(PYTHON_BINARY ${PYTHON_EXECUTABLE} CACHE STRING "")
  SET(PYTHON_LINKFLAGS "-Xlinker -export-dynamic")

  IF(WITH_SDL)
    FIND_PACKAGE(SDL)
    SET(SDL_INC ${SDL_INCLUDE_DIR})
    SET(SDL_LIB ${SDL_LIBRARY})
  ENDIF(WITH_SDL)

  FIND_PATH(OPENEXR_INC
    ImfXdr.h
    PATHS
    /usr/local/include/OpenEXR
    /usr/include/OpenEXR
    /sw/include/OpenEXR
    /opt/local/include/OpenEXR
    /opt/csw/include/OpenEXR
    /opt/include/OpenEXR
  )
  SET(OPENEXR_LIB Half IlmImf Iex Imath)

  SET(FFMPEG /usr)
  SET(FFMPEG_INC ${FFMPEG}/include)
  SET(FFMPEG_LIB avformat avcodec avutil avdevice swscale)
  SET(FFMPEG_LIBPATH ${FFMPEG}/lib)
  
  IF(WITH_FFTW3)
	  SET(FFTW3 /usr)
	  SET(FFTW3_INC ${FFTW3}/include)
	  SET(FFTW3_LIB fftw3)
	  SET(FFTW3_LIBPATH ${FFTW3}/lib)
  ENDIF(WITH_FFTW3)

  SET(LIBSAMPLERATE /usr)
  SET(LIBSAMPLERATE_INC ${LIBSAMPLERATE}/include)
  SET(LIBSAMPLERATE_LIB samplerate)
  SET(LIBSAMPLERATE_LIBPATH ${LIBSAMPLERATE}/lib)

  FIND_PACKAGE(JPEG REQUIRED)

  FIND_PACKAGE(PNG REQUIRED)

  FIND_PACKAGE(ZLIB REQUIRED)

  SET(LLIBS "-lXi -lutil -lc -lm -lpthread -lstdc++ -lX11 -ldl")

  IF(WITH_OPENMP)
    SET(LLIBS "${LLIBS} -lgomp")
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -fopenmp")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fopenmp")
  ENDIF(WITH_OPENMP)


  SET(PLATFORM_CFLAGS "-pipe -fPIC -funsigned-char -fno-strict-aliasing -DXP_UNIX -Wno-char-subscripts")

  SET(PLATFORM_LINKFLAGS "-pthread")

  # Better warnings
  SET(C_WARNINGS "-Wall -Wno-char-subscripts -Wpointer-arith -Wcast-align -Wnested-externs -Wdeclaration-after-statement")

  INCLUDE_DIRECTORIES(${JPEG_INCLUDE_DIR} ${PNG_INCLUDE_DIR} ${ZLIB_INCLUDE_DIR} )
ENDIF(UNIX AND NOT APPLE)

IF(WIN32)

  # this file is included anyway when building under Windows with cl.exe
  #  INCLUDE(${CMAKE_ROOT}/Modules/Platform/Windows-cl.cmake)
  
  SET(LIBDIR ${CMAKE_SOURCE_DIR}/../lib/windows)

  # Setup 64bit and 64bit windows systems
  IF(CMAKE_CL_64)
    message("64 bit compiler detected.")
    SET(LIBDIR ${CMAKE_SOURCE_DIR}/../lib/win64)
  ENDIF(CMAKE_CL_64)

  SET(PYTHON ${LIBDIR}/python)
  SET(PYTHON_VERSION 3.1)
  SET(PYTHON_INC "${PYTHON}/include/python${PYTHON_VERSION}")
  SET(PYTHON_BINARY python)
  SET(PYTHON_LIB python31)
  SET(PYTHON_LIBPATH ${PYTHON}/lib)

  IF(CMAKE_CL_64)
    SET(WITH_OPENAL OFF)
  ELSE(CMAKE_CL_64)
	#SET(WITH_OPENAL ON)
	SET(OPENAL ${LIBDIR}/openal)
	SET(OPENAL_INC ${OPENAL}/include)
	SET(OPENAL_LIB wrap_oal)
	SET(OPENAL_LIBPATH ${OPENAL}/lib)
  ENDIF(CMAKE_CL_64)

  IF(CMAKE_CL_64)
	SET(PNG_LIBRARIES libpng)
  ELSE(CMAKE_CL_64)
    SET(PNG_LIBRARIES libpng_st)
  ENDIF(CMAKE_CL_64)
  SET(JPEG_LIBRARY libjpeg)

  SET(ZLIB ${LIBDIR}/zlib)
  SET(ZLIB_INC ${ZLIB}/include)
  SET(ZLIB_LIBRARIES libz)
  SET(ZLIB_LIBPATH ${ZLIB}/lib)
  
  SET(PTHREADS ${LIBDIR}/pthreads)
  SET(PTHREADS_INC ${PTHREADS}/include)
  SET(PTHREADS_LIB pthreadVC2)
  SET(PTHREADS_LIBPATH ${PTHREADS}/lib)
  
  SET(ICONV ${LIBDIR}/iconv)
  SET(ICONV_INC ${ICONV}/include)
  SET(ICONV_LIB iconv)
  SET(ICONV_LIBPATH ${ICONV}/lib)
  
  IF(WITH_FFTW3)
	  SET(FFTW3 ${LIBDIR}/fftw3)
	  SET(FFTW3_INC ${FFTW3}/include)
	  SET(FFTW3_LIB libfftw)
	  SET(FFTW3_LIBPATH ${FFTW3}/lib)
  ENDIF(WITH_FFTW3)
  
  SET(GETTEXT ${LIBDIR}/gettext)
  SET(GETTEXT_INC ${GETTEXT}/include)
  IF(CMAKE_CL_64)
  	SET(GETTEXT_LIB gettext)
  ELSE(CMAKE_CL_64)
  	SET(GETTEXT_LIB gnu_gettext)
  ENDIF(CMAKE_CL_64)
  SET(GETTEXT_LIBPATH ${GETTEXT}/lib)

  SET(FREETYPE ${LIBDIR}/freetype)
  SET(FREETYPE_INC ${FREETYPE}/include ${FREETYPE}/include/freetype2)
  SET(FREETYPE_LIBPATH ${FREETYPE}/lib)
  SET(FREETYPE_LIB freetype2ST)
  
  SET(OPENEXR ${LIBDIR}/openexr)
  SET(OPENEXR_INC ${OPENEXR}/include ${OPENEXR}/include/IlmImf ${OPENEXR}/include/Iex ${OPENEXR}/include/Imath)
  SET(OPENEXR_LIB Iex Half IlmImf Imath IlmThread)
  IF (MSVC80)
  SET(OPENEXR_LIBPATH ${OPENEXR}/lib_vs2005)
  ELSE (MSVC80)
  SET(OPENEXR_LIBPATH ${OPENEXR}/lib_msvc)
  ENDIF(MSVC80)
  IF (MSVC90)
  SET(OPENEXR_LIBPATH ${OPENEXR}/lib_vs2008)
  ENDIF(MSVC90)

  
  SET(QUICKTIME ${LIBDIR}/QTDevWin)
  SET(QUICKTIME_INC ${QUICKTIME}/CIncludes)
  SET(QUICKTIME_LIB qtmlClient)
  SET(QUICKTIME_LIBPATH ${QUICKTIME}/Libraries)

  SET(FFMPEG ${LIBDIR}/ffmpeg)
  SET(FFMPEG_INC ${FFMPEG}/include)
  SET(FFMPEG_LIB avcodec-52 avformat-52 avdevice-52 avutil-50 swscale-0)
  SET(FFMPEG_LIBPATH ${FFMPEG}/lib)

  SET(LIBSAMPLERATE ${LIBDIR}/samplerate)
  SET(LIBSAMPLERATE_INC ${LIBSAMPLERATE}/include)
  SET(LIBSAMPLERATE_LIB libsamplerate)
  SET(LIBSAMPLERATE_LIBPATH ${LIBSAMPLERATE}/lib)

  IF(CMAKE_CL_64)
  SET(LLIBS kernel32 user32 vfw32 winmm ws2_32 )
  ELSE(CMAKE_CL_64)
  SET(LLIBS kernel32 user32 gdi32 comdlg32 advapi32 shell32 ole32 oleaut32 uuid ws2_32 vfw32 winmm)
  ENDIF(CMAKE_CL_64)

  SET(CMAKE_CXX_FLAGS_DEBUG "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /D_DEBUG /Od /Gm /EHsc /RTC1 /MTd /W3 /nologo /ZI /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_CXX_FLAGS_RELEASE "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /O2 /Ob2 /DNDEBUG /EHsc /MT /W3 /nologo /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_CXX_FLAGS_MINSIZEREL "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /O1 /Ob1 /DNDEBUG /EHsc /MT /W3 /nologo /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_CXX_FLAGS_RELWITHDEBINFO "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /O2 /Ob1 /DNDEBUG /EHsc /MT /W3 /nologo /Zi /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_C_FLAGS_DEBUG "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /D_DEBUG /Od /Gm /EHsc /RTC1 /MTd /W3 /nologo /ZI /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_C_FLAGS_RELEASE "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /O2 /Ob2 /DNDEBUG /EHsc /MT /W3 /nologo /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_C_FLAGS_MINSIZEREL "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /O1 /Ob1 /DNDEBUG /EHsc /MT /W3 /nologo /J" CACHE STRING "MSVC MT flags " FORCE)
  SET(CMAKE_C_FLAGS_RELWITHDEBINFO "/D_CRT_NONSTDC_NO_DEPRECATE /D_CRT_SECURE_NO_DEPRECATE /D_SCL_SECURE_NO_DEPRECATE /wd4800 /wd4244 /wd4305 /O2 /Ob1 /DNDEBUG /EHsc /MT /W3 /nologo /Zi /J" CACHE STRING "MSVC MT flags " FORCE)

  IF(WITH_OPENMP)
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /openmp ")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /openmp ")
  ENDIF(WITH_OPENMP)

  SET(SDL ${LIBDIR}/sdl)
  SET(SDL_INC ${SDL}/include)
  SET(SDL_LIB SDL)
  SET(SDL_LIBPATH ${SDL}/lib)

  SET(PNG "${LIBDIR}/png")
  SET(PNG_INC "${PNG}/include")
  SET(PNG_LIBPATH ${PNG}/lib)

  SET(JPEG "${LIBDIR}/jpeg")
  SET(JPEG_INC "${JPEG}/include")
  SET(JPEG_LIBPATH ${JPEG}/lib)

  SET(TIFF ${LIBDIR}/tiff)
  SET(TIFF_INC ${TIFF}/include)
  
  SET(WINTAB_INC ${LIBDIR}/wintab/include) 

  IF(CMAKE_CL_64)
  SET(PLATFORM_LINKFLAGS "/MANIFEST:NO /MANIFESTUAC:NO /MACHINE:X64 /NODEFAULTLIB:libc.lib;MSVCRT.lib ")
  ELSE(CMAKE_CL_64)
  SET(PLATFORM_LINKFLAGS "/NODEFAULTLIB:libc.lib ")
  ENDIF(CMAKE_CL_64)
  SET(CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG} /NODEFAULTLIB:libcmt.lib;libc.lib ")
ENDIF(WIN32)

IF(APPLE)
  IF(CMAKE_OSX_ARCHITECTURES MATCHES i386)
    SET(LIBDIR ${CMAKE_SOURCE_DIR}/../lib/darwin-8.x.i386)
  ELSE(CMAKE_OSX_ARCHITECTURES MATCHES i386)
    SET(LIBDIR ${CMAKE_SOURCE_DIR}/../lib/darwin-6.1-powerpc)
  ENDIF(CMAKE_OSX_ARCHITECTURES MATCHES i386)

  IF(WITH_OPENAL)
    FIND_PACKAGE(OpenAL)
    IF(OPENAL_FOUND)
      SET(WITH_OPENAL ON)
      SET(OPENAL_LIB ${OPENAL_LIBRARY})
      SET(OPENAL_INC ${OPENAL_INCLUDE_DIR})
    ELSE(OPENAL_FOUND)
      SET(WITH_OPENAL OFF)
    ENDIF(OPENAL_FOUND)
  ENDIF(WITH_OPENAL)


  SET(PYTHON_VERSION 3.1)

  IF(PYTHON_VERSION MATCHES 3.1)
    # we use precompiled libraries for py 3.1 and up by default

    SET(PYTHON ${LIBDIR}/python)
    SET(PYTHON_INC "${PYTHON}/include/python${PYTHON_VERSION}" CACHE STRING "")
    SET(PYTHON_BINARY "${PYTHON}/bin/python${PYTHON_VERSION}" CACHE STRING "")
    SET(PYTHON_LIB python${PYTHON_VERSION})
    SET(PYTHON_LIBPATH "${PYTHON}/lib/python${PYTHON_VERSION}" CACHE STRING "")
    #    SET(PYTHON_LINKFLAGS "-u _PyMac_Error")  # won't  build with this enabled
  ELSE(PYTHON_VERSION MATCHES 3.1)
    # otherwise, use custom system framework

    SET(PYTHON /System/Library/Frameworks/Python.framework/Versions/)
    SET(PYTHON_VERSION 2.5)
    SET(PYTHON_INC "${PYTHON}${PYTHON_VERSION}/include/python${PYTHON_VERSION}" CACHE STRING "")
    SET(PYTHON_BINARY ${PYTHON}${PYTHON_VERSION}/bin/python${PYTHON_VERSION} CACHE STRING "")
    SET(PYTHON_LIB "")
    SET(PYTHON_LIBPATH ${PYTHON}${PYTHON_VERSION}/lib/python${PYTHON_VERSION}/config CACHE STRING "")
    SET(PYTHON_LINKFLAGS "-u _PyMac_Error -framework System -framework Python")
  ENDIF(PYTHON_VERSION MATCHES 3.1)

  SET(GETTEXT ${LIBDIR}/gettext)
  SET(GETTEXT_INC "${GETTEXT}/include")
  SET(GETTEXT_LIB intl iconv)
  SET(GETTEXT_LIBPATH ${GETTEXT}/lib)
  
  IF(WITH_FFTW3)
    SET(FFTW3 ${LIBDIR}/fftw3)
	SET(FFTW3_INC ${FFTW3}/include)
	SET(FFTW3_LIB libfftw)
	SET(FFTW3_LIBPATH ${FFTW3}/lib)
  ENDIF(WITH_FFTW3)

  SET(PNG_LIBRARIES png)
  SET(JPEG_LIBRARY jpeg)

  SET(ZLIB /usr)
  SET(ZLIB_INC "${ZLIB}/include")
  SET(ZLIB_LIBRARIES z)

  SET(FREETYPE ${LIBDIR}/freetype)
  SET(FREETYPE_INC ${FREETYPE}/include ${FREETYPE}/include/freetype2)
  SET(FREETYPE_LIBPATH ${FREETYPE}/lib)
  SET(FREETYPE_LIB freetype)

  SET(OPENEXR ${LIBDIR}/openexr)
  SET(OPENEXR_INC ${OPENEXR}/include/OpenEXR ${OPENEXR}/include)
  SET(OPENEXR_LIB Iex Half IlmImf Imath IlmThread)
  SET(OPENEXR_LIBPATH ${OPENEXR}/lib)

  SET(FFMPEG ${LIBDIR}/ffmpeg)
  SET(FFMPEG_INC ${CMAKE_SOURCE_DIR}/extern/ffmpeg)
  SET(FFMPEG_LIB avcodec avdevice avformat avutil mp3lame swscale x264 xvidcore)
  SET(FFMPEG_LIBPATH ${FFMPEG}/lib)

  SET(LIBSAMPLERATE ${LIBDIR}/samplerate)
  SET(LIBSAMPLERATE_INC ${LIBSAMPLERATE}/include)
  SET(LIBSAMPLERATE_LIB samplerate)
  SET(LIBSAMPLERATE_LIBPATH ${LIBSAMPLERATE}/lib)

  SET(LLIBS stdc++ SystemStubs)

  SET(PLATFORM_CFLAGS "-pipe -fPIC -funsigned-char -fno-strict-aliasing")
  SET(PLATFORM_LINKFLAGS "-fexceptions -framework CoreServices -framework Foundation -framework IOKit -framework AppKit -framework Carbon -framework AGL -framework AudioUnit -framework AudioToolbox -framework CoreAudio -framework QuickTime")

  IF(WITH_OPENMP)
    SET(LLIBS "${LLIBS} -lgomp ")
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -fopenmp ")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fopenmp ")
  ENDIF(WITH_OPENMP)

  SET(SDL ${LIBDIR}/sdl)
  SET(SDL_INC ${SDL}/include)
  SET(SDL_LIB SDL)
  SET(SDL_LIBPATH ${SDL}/lib)

  SET(PNG "${LIBDIR}/png")
  SET(PNG_INC "${PNG}/include")
  SET(PNG_LIBPATH ${PNG}/lib)

  SET(JPEG "${LIBDIR}/jpeg")
  SET(JPEG_INC "${JPEG}/include")
  SET(JPEG_LIBPATH ${JPEG}/lib)

  SET(TIFF ${LIBDIR}/tiff)
  SET(TIFF_INC ${TIFF}/include)

  SET(EXETYPE MACOSX_BUNDLE)
ENDIF(APPLE)

IF(CMAKE_SYSTEM_NAME MATCHES "Linux")
  SET(BINRELOC ${CMAKE_SOURCE_DIR}/extern/binreloc)
  SET(BINRELOC_INC ${BINRELOC}/include)
ENDIF(CMAKE_SYSTEM_NAME MATCHES "Linux")

#-----------------------------------------------------------------------------
# Common.

set(OPENJPEG ${CMAKE_SOURCE_DIR}/extern/libopenjpeg)
set(OPENJPEG_INC ${OPENJPEG})
set(OPENJPEG_LIb extern_libopenjpeg)

#-----------------------------------------------------------------------------
# Blender WebPlugin

IF(WITH_WEBPLUGIN) 
  SET(GECKO_DIR "${CMAKE_SOURCE_DIR}/../gecko-sdk/" CACHE PATH "Gecko SDK path")
  SET(WEBPLUGIN_SANDBOX_MODE "apparmor" CACHE STRING "WEB Plugin sandbox mode, can be apparmor, privsep, none")

  SET(WITH_PLAYER ON)
ENDIF(WITH_WEBPLUGIN)


#-----------------------------------------------------------------------------
# Configure OpenGL.
FIND_PACKAGE(OpenGL)
INCLUDE_DIRECTORIES(${OPENGL_INCLUDE_DIR})
#-----------------------------------------------------------------------------
# Extra compile flags
IF(WITH_GAMEENGINE)
  SET(PLATFORM_CFLAGS "${PLATFORM_CFLAGS} -DGAMEBLENDER ")
ENDIF(WITH_GAMEENGINE)
IF(WITH_BULLET)
  SET(PLATFORM_CFLAGS "${PLATFORM_CFLAGS} -DWITH_BULLET ")
ENDIF(WITH_BULLET)
SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${PLATFORM_CFLAGS} ${C_WARNINGS}")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${PLATFORM_CFLAGS} ")

#-----------------------------------------------------------------------------
# Libraries
FILE(WRITE ${CMAKE_BINARY_DIR}/cmake_blender_libs.txt "")
ADD_SUBDIRECTORY(intern)
ADD_SUBDIRECTORY(extern)
ADD_SUBDIRECTORY(source)


#-----------------------------------------------------------------------------
# Blender Application
ADD_SUBDIRECTORY(source/creator)


#-----------------------------------------------------------------------------
# Blender Player
IF(WITH_PLAYER)
  ADD_SUBDIRECTORY(blenderplayer)
ENDIF(WITH_PLAYER)
