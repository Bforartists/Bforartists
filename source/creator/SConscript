#!/usr/bin/python
Import ('env')
import os

sources = 'creator.c'

incs = '#/intern/guardedalloc ../blender/blenlib ../blender/blenkernel'
incs += ' ../blender/editors/include ../blender/blenloader ../blender/imbuf'
incs += ' ../blender/renderconverter ../blender/render/extern/include ../blender/windowmanager'
incs += ' ../blender/makesdna ../blender/makesrna ../kernel/gen_messaging'
incs += ' ../kernel/gen_system #/extern/glew/include ../blender/gpu'
incs += ' ' + env['BF_OPENGL_INC']

defs = []
if env['WITH_BF_QUICKTIME']:
	incs += ' ' + env['BF_QUICKTIME_INC']
	defs.append('WITH_QUICKTIME')

if env['WITH_BF_BINRELOC']:
	incs += ' ../../extern/binreloc/include'
	defs.append('WITH_BINRELOC')

if env['WITH_BF_OPENEXR']:
	defs.append('WITH_OPENEXR')

if not env['WITH_BF_SDL']:
	defs.append('DISABLE_SDL')

if env['WITH_BF_PYTHON']:
	incs += ' ../blender/python'
	if env['BF_DEBUG']:
		defs.append('_DEBUG')
else:
	defs.append('DISABLE_PYTHON')

if env['WITH_BF_FHS']: # /usr -> /usr/share/blender/2.5
	defs.append('BLENDERPATH=\\"' + os.path.join(env['BF_INSTALLDIR'], 'share', 'blender', env['BF_VERSION']) + '\\"')
	
if env['BF_BUILDINFO']:
	defs.append('BUILD_DATE')
	defs.append('NAN_BUILDINFO')

env.BlenderLib ( libname = 'bf_creator', sources = Split(sources), includes = Split(incs), defines = defs, libtype='core', priority = 0 )
