#!/usr/bin/python
import sys
import os

Import ('env')
cflags = ''
defines = []
root_build_dir=env['BF_BUILDDIR']

source_files = ['makesdna.c', '#intern/guardedalloc/intern/mallocn.c']

makesdna_tool = env.Copy()
dna = env.Copy()
makesdna_tool.Append(CCFLAGS = '-DBASE_HEADER="\\"source/blender/makesdna/\\"" ')

makesdna_tool.Append (CPPPATH = ['#/intern/guardedalloc',
                                 '../../makesdna'])

if env['OURPLATFORM'] == 'linuxcross':
    makesdna_tool.Replace(CC='gcc')
    makesdna_tool.Replace(AR='ar')
    makesdna_tool.Replace(LINK='gcc')

if sys.platform != 'cygwin':
    makesdna_tool.Append (CCFLAGS = cflags)
makesdna_tool.Append (CPPDEFINES = defines)
makesdna_tool.Append (LIBPATH = '#'+root_build_dir+'/lib')

makesdna_tool.Program (target = '#'+root_build_dir+os.sep+'makesdna', source = source_files)

dna_dict = dna.Dictionary()
makesdna_name = root_build_dir+'/makesdna' + dna_dict['PROGSUFFIX']
dna.Depends ('dna.c', '#'+makesdna_name)
if env['OURPLATFORM'] != 'linuxcross':
    dna.Command ('dna.c', '', root_build_dir+os.sep+"makesdna $TARGET")
else:
    dna.Command ('dna.c', '', root_build_dir+os.sep+"makesdna.exe $TARGET")
obj = 'intern/dna.c'
Return ('obj')
