#!/usr/bin/python
Import ('env')

sources = env.Glob('intern/*.c')
if env['WITH_GHOST_COCOA']:
	sources += env.Glob('intern/*.m')
	sources.remove('intern/dynlibtiff.c')
	sources.remove('intern/tiff.c')

incs = '. ../makesdna #/intern/guardedalloc #/intern/memutil ../blenlib'
incs += ' ../avi ../blenkernel'


incs += ' ' + env['BF_JPEG_INC']
incs += ' ' + env['BF_PNG_INC']
incs += ' ' + env['BF_TIFF_INC']
incs += ' ' + env['BF_ZLIB_INC']

defs = []

if env['WITH_BF_OPENEXR']:
    defs.append('WITH_OPENEXR')

if env['WITH_BF_DDS']:
    defs.append('WITH_DDS')

if env['WITH_BF_FFMPEG']:
    defs.append('WITH_FFMPEG')
    incs += ' ' + env['BF_FFMPEG_INC']

if env['WITH_BF_OPENJPEG']:
    defs.append('WITH_OPENJPEG')
    incs += ' ' + env['BF_OPENJPEG_INC']

if env['WITH_BF_REDCODE']:
    defs.append('WITH_REDCODE')
    incs += ' ' + env['BF_REDCODE_INC']

if env['WITH_BF_QUICKTIME']:
	incs += ' ../quicktime ' + env['BF_QUICKTIME_INC']
	defs.append('WITH_QUICKTIME')

env.BlenderLib ( libname = 'bf_imbuf', sources = sources, includes = Split(incs), defines = defs, libtype=['core','player'], priority = [185,115] )
